/*--------------------dnAnimationTimer v1.0.1--------------------Modified: July 16, 2004--------------------Danny Ngand@dannyngan.comhttp://www.dannyngan.com--------------------

DESCRIPTION:

This script creates a floating window that allows you to time animations
and tap out timing intervals. Keyframe times and intervals between keys
are printed in the Script Editor.

Note: Because of the rounding of values and conversion from time to frames,
some of the interval results may be off by a frame.

Also, this script and Chris Fram's cfStopWatch.mel are very similar. This
is just a coincidence that both Chris and I have acknowledged. We are like
Coke and Pepsi. :)

-------------------

INSTALLATION:

Place "dnAnimationTimer.mel" into the user scripts directory:

	OSX: ~/Library/Preferences/AliasWavefront/maya/scripts/
	Windows: My Documents/maya/scripts/

The optional shelf icons can be placed in the user icons directory:

	OSX: ~/Library/Preferences/AliasWavefront/maya/5.0/prefs/icons/	Windows: My Documents/maya/5.0/prefs/icons/
	
There are versions for both OSX (.xpm) and Windows (.bmp).

-------------------


USAGE:

Run the following command from Maya's command line or the script editor:

	dnAnimationTimer;

Click the "Start/Stop" button to begin counting the frames. Each time the "Key"
button is pressed, the script prints out the current time in frames and the
elapsed time in frames since the last previous key. Frame counts are based on
the frame rate set in the "FPS" text field.

--------------------Changelist:

v1.0.1 (July 16, 2004)
- Simplified Script Editor output.

v1.0 (July 14, 2004)- Initial release.--------------------

*/

global proc startTime()
{
	// init global variables
	global float $startTime;
	global float $prevTime;
	global float $currTime;
	global int $fps;
	
	$fps = `intField -q -v framerate`;
	
	// get current time
	$startTime = `timerX`;
	$prevTime = $startTime;
	
	print ("Animation timer started.\n");
	print ("Timer running at " + $fps + " fps." + "\n");
}

global proc keyTime()
{
	global float $startTime;
	global float $prevTime;
	global float $currTime;
	global int $fps;
	
	// get current time
	$currTime = `timerX`;
	
	// calculate intervals
	float $keyTime = $currTime - $startTime;
	float $intTime = $currTime - $prevTime;
	
	// get actual frame numbers
	float $key = trunc(($keyTime * $fps) + 0.5);
	float $interval = trunc(($intTime * $fps) + 0.5);
	print ("Frame: " + $key + "     Æ: " + $interval + "\n");
	
	// set global time for next key button click
	$prevTime = $currTime;
}

global proc saveFramerate()
{
	// save the framerate between different sessions
	optionVar -intValue dn_at_framerate `intField -q -v framerate`;
}


global proc dnAboutWindow()
{
	// Kill any existing about windows.
	
	if ((`window -ex dnAboutWindow`) == true) deleteUI dnAboutWindow;
	if (`windowPref -ex dnAboutWindow`) windowPref -r dnAboutWindow;
		
	window
		-title "About"
		-rtf true
		-s false
		-width 128
		-height 96
		dnAboutWindow;
		
		columnLayout -adjustableColumn true -columnAttach "both" 10;
		
		separator -style "none" -h 10;
		
		text -label "Animation Timer\nversion 1.0.1\n©2004 by Danny Ngan\nhttp://www.dannyngan.com" -align "center";

		// Mac-specific layout options because of different line spacing
		
		if (`about -mac`) separator -style "none" -h 5;
		else separator -style "none" -h 10;
				
		button -label "Close" -w 128 -c "deleteUI dnAboutWindow;";

		separator -style "none" -h 10;
		
		setParent ..;
		
	showWindow dnAboutWindow;
}

global proc dnAnimationTimer()
{
	string $foo = "print \"bob\"";
	
	if (`window -q -ex animTimer`) deleteUI animTimer;
	
	window
		-title "Animation Timer"
		-width 192
		-height 128
		-mnb true
		-mxb false
		-rtf true
		-s false
		-menuBar true
		animTimer;
		
		menu -label "About" -tearOff false;
			menuItem
				-label "About Animation Timer..."
				-c "dnAboutWindow";
			menuItem -divider true;
			menuItem
				-label "Danny Ngan's website"
				-c "showHelp -a \"http://www.dannyngan.com\""
				-ann "Opens Danny Ngan's website.";	
				
		columnLayout
			-adj true
			-columnAlign "center"
			-columnWidth 96
			-cat "both" 10;
			
		separator -style "none" -height 10;
		
		rowLayout -numberOfColumns 2 -columnAlign2 "right" "left";
			text -label "FPS:" -width 96 -align "right";
			intField -width 48 -ed true -cc "saveFramerate" framerate;
			setParent ..;
		
		separator -style "none" -height 10;

		rowLayout -numberOfColumns 2 -columnAlign2 "center" "center";
			button
				-label "Start/Reset"
				-align "center"
				-width 96
				-height 96
				-c startTime;
			
			button
				-label "Key"
				-align "center"
				-width 96
				-height 96
				-c keyTime;
			setParent ..;
		
		separator -style "none" -height 10;
		
		button
			-label "Close"
			-c "deleteUI animTimer;";
		
		separator -style "none" -height 10;
		
		// get the current framerate
		$time = `currentUnit -q -time`;
		
		if ($time == "ntsc") $framerate = 30;
		else if ($time == "pal") $framerate = 25;
		else if ($time == "film") $framerate = 24;
		else if ($time == "game") $framerate = 15;
		else if ($time == "show") $framerate = 48;
		else if ($time == "palf") $framerate = 50;
		else if ($time == "ntscf") $framerate = 60;
		else $framerate = 30;	//for all other time-based framerates

		// get the previous framerate setting if it exists
		if (`optionVar -ex dn_at_framerate`)
			$framerate = `optionVar -q dn_at_framerate`;
		
		// set the FPS value in the window
		intField -e -v $framerate framerate;

		showWindow animTimer;
}